record Message = [int ID, int COUNTER];

fun Message newMessage(int id, int counter){
	Message mess = [ID = id, COUNTER = counter];
	return mess;
}

fun list<Message> addMessage(Message mess, list<Message> buf){
	buf = buf + [:mess:];
	return buf;
}

const N = 20;

component Agent(int position, int id){
	
	store{
		attrib int position = position;
		attrib int id = id;
		attrib int counter = 0;
		attrib int ringSize = 1;
		attrib int right = (position+1)%N;
		attrib int left = (position+N-1)%N;
		attrib int minimum = MAXINT;
		attrib bool known = false;
		attrib list<Message> buf = newList(Message);
	}
	
	behaviour{
		ASLEEP = 
			send[my.right == position]<my.id,my.counter>{
				my.minimum = my.id;
			}.AWAKE
			
			+
			
			send [my.left == position](rId,rCounter){
				my.buf = addMessage(newMessage(rId,rCounter),my.buf);
			}.FOLLOWER;
			
		AWAKE = 
			send[my.left == position](rId,rCounter){
				my.buf = addMessage(newMessage(rId,rCounter),my.buf);
			}.AWAKE
			
			+
		
			[(size(my.buf) > 0) && (my.buf[0].ID != my.id) && !my.known]send[my.right == position]<my.buf[0].ID,my.buf[0].COUNTER+1>{
				my.minimum = min(my.minimum, my.buf[0].ID);
				my.counter = my.counter + 1;
				my.buf = tail(my.buf);
			}.AWAKE
			
			+
			
			[(size(my.buf) > 0) && (my.buf[0].ID != my.id) && my.known]send[my.right == position]<my.buf[0].ID,my.buf[0].COUNTER+1>{
				my.minimum = min(my.minimum, my.buf[0].ID);
				my.counter = my.counter + 1;
				my.buf = tail(my.buf);
			}.CHECK
			
			+
			
			[(size(my.buf) > 0) && (my.buf[0].ID == my.id)]check*{
				my.ringSize = my.buf[0].COUNTER;
				my.known = true;
				my.buf = tail(my.buf);
			}.CHECK;			
			
		CHECK =
			[(my.counter == my.ringSize) && (my.id == my.minimum)]youWin*{}.LEADER
			
			+
			
			[(my.counter == my.ringSize) && (my.id != my.minimum)]youLose*{}.FOLLOWER;
			
		LEADER =
			winner*{}.LEADER;
			
		FOLLOWER = 
			send[my.left == position](rId,rCounter){
				my.buf = addMessage(newMessage(rId,rCounter),my.buf);
			}.FOLLOWER
			
			+
			
			[(size(my.buf) > 0)]send[my.right == position]<my.buf[0].ID,my.buf[0].COUNTER>{
				my.buf = tail(my.buf);
			}.FOLLOWER
			
			+
			
			loser*{}.FOLLOWER;
			
	}
	init{
		ASLEEP
	}
} 

system Ring {	
	collective {
		for (i ; i < N ; 1) {
			new Agent(i, 10*i);
		}
	}	
	environment {
		store {
			attrib int messages = 0;
			}
		update {
			send{
				messages = global.messages+1;
			}
		}
	}
}

measure leader = #{Agent[LEADER] | true};
measure followers = #{Agent[FOLLOWER] | true};
measure messages = global.messages;